#lang ivy1.7

#
# A queue for testing the interactive invariant construction tool
#

module total_order(t,r) = {
    axiom r(X:t,X)                        # Reflexivity
    axiom r(X:t, Y) & r(Y, Z) -> r(X, Z)  # Transitivity
    axiom r(X:t, Y) & r(Y, X) -> X = Y    # Anti-symmetry
    axiom r(X:t, Y) | r(Y, X)             # Totality
}

object incrementable = {

    type t
    relation (X:t <= Y:t)

    relation succ(X:t,Y:t)

    action next(input:t) returns (output:t) # No implementation given: it is an abstract module
    
    # Specifications of this abstract module
    object spec = {
	
		property succ(X,Z) -> (X < Z & ~(X < Y & Y < Z))

        instantiate total_order(t, <=) 

        after next {
            ensure input < output ;
			ensure succ(input, output);
        }
    }

    # Provide an implementation for this abstract module (=interface). Not needed if we just want to prove the lock protocol.
    object impl = {

		interpret t -> int

		definition succ(X,Y) = (Y = X + 1)

        implement next {
            output := input + 1;
        }
    }
}

module unbounded_queue(data) =
{
    individual next_e : incrementable.t
    relation content(D:data, E:incrementable.t)
    individual first_e : incrementable.t
    individual first : data

    after init
    {
		content(D,E) := false ;
		first_e := 0 ;
		next_e := 0;
    }

    derived empty = first_e >= next_e

    action push (da:data) =
    {
		if empty
		{
			first := da;
		} ;
		content(da, next_e) := true ;
		next_e := incrementable.next(next_e);
    }

    action pop returns (res:data) =
    {
		require ~empty ;
		res := first ;
		content(first, first_e) := false ;
		first_e := incrementable.next(first_e) ;
		if some nf:data. content(nf,first_e)
		{
			first := nf;
		};
    }

    object spec =
    {
		#function content_f(E:incrementable.t) : data
		#before push
		#{
		#	content_f(next_e) := da;
		#}

		conjecture ~empty -> content(first,first_e)
		conjecture content(A,first_e) -> A=first
		
		# Invariants found
		conjecture (A < q.next_e) & (q.first_e < A) & ~q.content(q.first, A) -> (exists B:data. q.content(B, A))
		conjecture ~q.content(B, q.next_e)
		conjecture ~(B >= q.next_e & q.content(A, B))
		conjecture ~(q.content(A, D) & q.content(B, D) & A ~= B)
	}

    export pop
    export push
}

type data
instance q: unbounded_queue(data)

isolate iso_incr = incrementable
isolate iso_queue = q with incrementable
